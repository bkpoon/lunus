 remarks  file  generate/generate.inp
 remarks  Generate structure file and hydrogens for a protein

 topology
    @TOPPAR:tophcsdx.pro 
 

 end  
 parameter 
 @TOPPAR:parhcsdx.pro                             {*Read empirical potential*}



   nbonds                                  {*This statement specifies the  *}
      atom cdie shift eps=1.0  e14fac=0.4  {*nonbonded interaction energy  *}
      cutnb=7.5 ctonnb=6.0 ctofnb=6.5      {*options.  Note the reduced    *}
      nbxmod=5 vswitch                     {*nonbonding cutoff to save some*}
   end                                     {*CPU time.   This statement    *}
                                           {*overwrites the defaults in    *}
                                           {*the parameter file.           *}
 end

 segment                                                {*Generate protein.*}

   name="1STN"                              {*This name has to match the   *}
                                            {*four characters in columns 73*}
                                            {*through 76 in the coordinate *}
                                            {*file; in XPLOR this name is  *}
                                            {*referred to as SEGId.        *}
   chain
     @TOPPAR:toph19.pep                      {*Read peptide bond file;     *}
     coordinates @stn.pdb
	{*interpret coordinate file to*}

   end
  end 


  coordinates @stn.pdb

  vector do (name="CD1") (name CD and resname ile)

flags exclude vdw elec end                {*Do QUICK hydrogen building w/o*}
                                           {*vdw and elec terms.           *}

 hbuild                                      {*This statement builds       *}
    selection=( hydrogen )                   {*missing hydrogens, which are*}
    phistep=45                               {*needed for the force field. *}
 end
 write coordinates output=generate.pdb end         {*Write out coordinates.*} 

 write structure output=generate.psf end      {*Write out structure file.*}
 stop
